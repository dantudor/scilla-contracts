
scilla_version 0




import BoolUtils ListUtils

library IgniteWallet

type Error =
| NotAuthorized
| SenderIsNotSSNList
| InvalidSRSetup
| SRNotSetup
| InvalidSR
| WalletLocked
| InvalidCheque
| PubkeysAreNotUnique
| WrongRecoveryListLength
| RecoveryMsgPreconditionNotMet
| InvalidSignature
| OtherHasNoAdmin

let make_error =
fun (result: Error) =>
let result_code =
match result with
| NotAuthorized => Int32 -1
| SenderIsNotSSNList => Int32 -2
| InvalidSRSetup => Int32 -3
| SRNotSetup => Int32 -4
| InvalidSR => Int32 -5
| WalletLocked => Int32 -6
| InvalidCheque => Int32 -7
| PubkeysAreNotUnique => Int32 -8
| WrongRecoveryListLength => Int32 -9
| RecoveryMsgPreconditionNotMet => Int32 -10
| InvalidSignature => Int32 -11
| OtherHasNoAdmin => Int32 -12
end
in
{ _exception: "Error"; code: result_code }

let one_msg = fun( msg: Message ) =>
let nil_msg = Nil{ Message } in Cons{ Message } msg nil_msg

let zero = Uint128 0
let uint32_zero = Uint32 0
let zeroByStr20 = 0x0000000000000000000000000000000000000000

let option_value = tfun 'A => fun( default: 'A ) => fun( input: Option 'A) =>
  match input with
  | Some v => v
  | None => default end
let option_uint128_value = let f = @option_value Uint128 in f zero
let option_bystr20_value = let f = @option_value ByStr20 in f zeroByStr20


let commission_denominator = Uint128 10000

type Uint128Pair =
| Uint128Pair of Uint128 Uint128
let uint128_to_uint256 : Uint128 -> Uint256 =
  fun (x : Uint128) =>
    let ox256 = builtin to_uint256 x in
      match ox256 with
      | None =>
        
        let zero = Uint256 0 in
        builtin div zero zero
      | Some x256 => x256
      end

let muldiv : Uint128 -> Uint128 -> Uint128 -> Uint128 =
    fun (x : Uint128) =>
    fun (y : Uint128) =>
    fun (z : Uint128) =>
      let x256 = uint128_to_uint256 x in
      let y256 = uint128_to_uint256 y in
      let z256 = uint128_to_uint256 z in
      let x_mul_y256 = builtin mul x256 y256 in
      let res256 = builtin div x_mul_y256 z256 in
      let ores128 = builtin to_uint128 res256 in
      match ores128 with
      | None =>
        
        let max_uint128 = Uint128 340282366920938463463374607431768211455 in
        let fourtytwo128 = Uint128 42 in
        builtin mul max_uint128 fourtytwo128
      | Some res128 =>
        res128
      end

let sub_percentage_of_amt: Uint128 -> Uint128 -> Uint128Pair =
    fun (amount: Uint128) => fun (commission_numerator: Uint128) =>
    let commission = muldiv amount commission_numerator commission_denominator in
    let amount_sub_commission = builtin sub amount commission in
    let res = Uint128Pair amount_sub_commission commission in
    res

type Quadruplet =
| Quadruplet of Uint128 Uint128 Uint128 Uint128

let zilswap_params_sub_commission_with_discount: Uint128 -> Uint128 -> Uint128 -> Uint128 -> Quadruplet =
    fun (total1: Uint128) => fun (total2: Uint128) => fun (comm: Uint128) => fun (reputation_discount: Uint128) =>
    let res0 = sub_percentage_of_amt comm reputation_discount in
    match res0 with
    | Uint128Pair smaller_comm discount =>
        let res1 = sub_percentage_of_amt total1 smaller_comm in
        let res2 = sub_percentage_of_amt total2 smaller_comm in
        match res1 with
        | Uint128Pair amt1 comm1 =>
            match res2 with
            | Uint128Pair amt2 comm2 => Quadruplet amt1 comm1 amt2 comm2
            end
        end
    end

let add_funds = "AddFunds"
let false = False
let true = True
let one_zil = Uint128 1000000000000


type SocialRecoverySet =
| NotSetup
| Setup of (List ByStr33) Uint32


type RecovererSignedMsg =
| RecovererSignedMsg of ByStr33 BNum ByStr64

let bystr33_list_len: List ByStr33 -> Uint32 = 
    fun (l : List ByStr33) =>
        let len = @list_length ByStr33 in len l
let recoverer_msg_list_len: List RecovererSignedMsg -> Uint32 = 
    fun (l : List RecovererSignedMsg) =>
        let len = @list_length RecovererSignedMsg in
        len l
let bystr33_has_elem: List ByStr33 -> ByStr33 -> Bool = 
    fun (l : List ByStr33) => fun (e : ByStr33) =>
        let bystr33_eq = fun(e1: ByStr33) => fun(e2: ByStr33) => builtin eq e1 e2 in
        let curried = bystr33_eq e in
        let has = @list_exists ByStr33 in
        has curried l
let pubkeys_are_unique: List ByStr33 -> Bool =
    fun(msgs: List ByStr33) =>
    let initial_len = bystr33_list_len msgs in
    let init_map: Map ByStr33 Bool = Emp ByStr33 Bool in
    let put_in_map: Map ByStr33 Bool -> ByStr33 -> Option Map ByStr33 Bool =
        fun(acc: Map ByStr33 Bool) => fun(pubkey: ByStr33) =>
            let tmp_acc = builtin put acc pubkey true in Some {Map ByStr33 Bool} tmp_acc in
    let fold = @list_foldl_while ByStr33 Map ByStr33 Bool in
    let pubkey_set = fold put_in_map init_map msgs in
    let pubkey_set_list = builtin to_list pubkey_set in
    let len = @list_length (Pair ByStr33 Bool) in
    let pubkey_set_list_len = len pubkey_set_list in
    builtin eq pubkey_set_list_len initial_len
let recoverer_msg_list_has_unique_pubkeys: List RecovererSignedMsg -> Bool =
    fun(msgs: List RecovererSignedMsg) =>
    let mapper = fun(msg: RecovererSignedMsg) => match msg with | RecovererSignedMsg pubkey valid sig => pubkey end in
    let map = @list_map RecovererSignedMsg ByStr33 in
    let mapped = map mapper msgs in
    pubkeys_are_unique mapped

let ssn_proxy = "ssn_proxy_v1"
let fee_collector = "fee_collector_v1"
let zilswap = "zilswap_v1"
let ignite_service_account = "ignite_service_account_v1"
let ssnlist_implementation = "ssnlist_v1"
let cookie_jar = "cookie_jar_v1"

contract IgniteWallet(
    init_admin_pubkey: ByStr33,
    init_config: ByStr20 with contract field config_bystr20: Map String ByStr20, field config_uint128: Map String Uint128, field reputation: ByStr20 with contract field address_to_discount: Map ByStr20 Uint128 end end
)



field bystr20_read: ByStr20 = zeroByStr20
field uint128_read: Uint128 = zero



field admin: ByStr20 = builtin schnorr_get_address init_admin_pubkey
field admin_pubkey: ByStr33 = init_admin_pubkey




field recoverers: SocialRecoverySet = NotSetup

field is_locked: Bool = False

field temp_nominated_admin: ByStr20 = zeroByStr20



field void_cheques: Map ByStr Bool = Emp ByStr Bool

field temp_discount: Uint128 = zero

field temp_was_service_account: Bool = false

procedure ThrowError(err: Error)
    e = make_error err;
    throw e
end


procedure WalletIsNotLocked()
    is_locked_tmp <- is_locked;
    match is_locked_tmp with
    | True => e = WalletLocked; ThrowError e
    | False => 
    end
end





procedure ReadByStr20Config(key: String)
    option_service <-& init_config.config_bystr20[key];
    service = option_bystr20_value option_service;
    bystr20_read := service
end

procedure ReadUint128Config(key: String)
    option_val <-& init_config.config_uint128[key];
    val = option_uint128_value option_val;
    uint128_read := val
end
procedure AssertIsAdmin(addr: ByStr20)
    admin_temp <- admin;
    is_admin = builtin eq admin_temp addr;
    match is_admin with
    | True =>
    | False => e = NotAuthorized; ThrowError e
    end
end

procedure IsAdmin()
    AssertIsAdmin _sender
end

procedure IsAdminOrServiceAccount()
    admin_temp <- admin;
    is_admin = builtin eq admin_temp _sender;
    ReadByStr20Config ignite_service_account; service <- bystr20_read;
    is_service_account = builtin eq service _sender;
    is_admin_or_service = orb is_service_account is_admin;
    match is_admin_or_service with
    | True =>
    | False => e = NotAuthorized; ThrowError e
    end
end




procedure AssertChequeValidityAndVoidIt(cheque_hash: ByStr, pubkey: ByStr33, signature: ByStr64)
    cheque_invalid <- exists void_cheques[cheque_hash];
    match cheque_invalid with 
        | False =>
            valid_sig = builtin schnorr_verify pubkey cheque_hash signature;
            match valid_sig with
                | True => 
                    
                    
                    void_cheques[cheque_hash] := true
                | False => e = InvalidSignature; ThrowError e
            end
        | True => e = InvalidCheque; ThrowError e
    end
end

procedure UpdateDiscount()
    rep <-& init_config.reputation;
    option_discount <-& rep.address_to_discount[_this_address];
    discount = option_uint128_value option_discount;
    temp_discount := discount
end

procedure SendZIL(tag: String, beneficiary: ByStr20, amt: Uint128)
    msg = let m = { 
        _tag: tag;
        _recipient: beneficiary;
        _amount: amt
    } in one_msg m;
    send msg
end

procedure TransferMsg(impl: ByStr20, beneficiary: ByStr20, amt: Uint128)
    msg = let m = {
        _tag: "Transfer";
        _recipient: impl;
        _amount: zero;
        to: beneficiary;
        amount: amt 
    } in one_msg m;
    send msg
end

procedure PayZILFee(amt: Uint128)
    ReadByStr20Config fee_collector; collector <- bystr20_read;
    SendZIL add_funds collector amt
end

procedure PayTokenFee(token_address: ByStr20, amt: Uint128)
    ReadByStr20Config fee_collector; collector <- bystr20_read;
    TransferMsg token_address collector amt
end

procedure IncreaseAllowance(impl: ByStr20, spender: ByStr20, amt: Uint128)
    msg = let m = {
        _tag: "IncreaseAllowance";
        _recipient: impl;
        _amount: zero;
        spender: spender;
        amount: amt
    } in one_msg m;
    send msg
end

procedure PaybackForGas(tag: String)
    ReadUint128Config tag; gas_back <- uint128_read;
    is_zero = builtin eq zero gas_back;
    match is_zero with
    | True =>
    | False => SendZIL add_funds _sender gas_back
    end
end

transition SendFunds(tag: String, beneficiary: ByStr20, amt: Uint128)
    g = "SendFunds";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    SendZIL tag beneficiary amt
end

transition Transfer(impl: ByStr20, beneficiary: ByStr20, amt: Uint128)
    g = "Transfer";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    TransferMsg impl beneficiary amt
end





procedure SsnProxyAddrMsg(tag: String, ssnaddr: ByStr20, amt: Uint128)
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    msg = let m = { 
        _tag: tag;
        _recipient: proxy;
        _amount: amt;
        ssnaddr: ssnaddr
    } in one_msg m;
    send msg
end

procedure SsnProxyPlainMsg(tag: String)
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    msg = let m = { 
        _tag: tag;
        _recipient: proxy;
        _amount: zero
    } in one_msg m;
    send msg
end

procedure SsnProxyRequestorMsg(tag: String, requestor: ByStr20)
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    msg = let m = { 
        _tag: tag;
        _recipient: proxy;
        _amount: zero;
        requestor: requestor
    } in one_msg m;
    send msg
end
procedure DoWithdrawStakeRewards(ssnaddr: ByStr20)
    tag = "WithdrawStakeRewards";
    SsnProxyAddrMsg tag ssnaddr zero
end

transition WithdrawStakeRewards(ssnaddr: ByStr20)
    tag = "WithdrawStakeRewards";
    IsAdminOrServiceAccount; WalletIsNotLocked; PaybackForGas tag;
    ReadByStr20Config ignite_service_account; service <- bystr20_read;
    is_service_account = builtin eq _sender service;
    temp_was_service_account := is_service_account; 
    DoWithdrawStakeRewards ssnaddr
end

transition DelegateStake(ssnaddr: ByStr20, amt: Uint128)
    tag = "DelegateStake";
    IsAdminOrServiceAccount; WalletIsNotLocked; PaybackForGas tag;
    ReadByStr20Config ignite_service_account; service <- bystr20_read;
    is_service_account = builtin eq service _sender;
    fee_tag = "DelegateStake_fee";
    
    ReadUint128Config fee_tag; percentage_fee <- uint128_read;
    is_zero = builtin eq zero percentage_fee;
    not_zero = negb is_zero;
    is_service_account_fee_not_zero = andb is_service_account not_zero;
    
    match is_service_account_fee_not_zero with
    | True =>
        
        ReadByStr20Config fee_collector; collector <- bystr20_read;
        UpdateDiscount; discount <- temp_discount;
        fee_itself_discounted = sub_percentage_of_amt percentage_fee discount;
        match fee_itself_discounted with
        | Uint128Pair smaller_fee_percentage dis =>
            amt_to_send_and_fee = sub_percentage_of_amt amt smaller_fee_percentage;
            match amt_to_send_and_fee with
            | Uint128Pair to_send fee_to_pay =>
                SendZIL add_funds collector fee_to_pay;
                SsnProxyAddrMsg tag ssnaddr to_send
            end
        end
    | False => SsnProxyAddrMsg tag ssnaddr amt
    end
end

transition WithdrawStakeAmt(ssnaddr: ByStr20, amt: Uint128)
    tag = "WithdrawStakeAmt";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    msg = let m = { 
        _tag: tag;
        _recipient: proxy;
        _amount: zero;
        ssnaddr: ssnaddr;
        amt: amt
    } in one_msg m;
    send msg
end

transition CompleteWithdrawal()
    tag = "CompleteWithdrawal";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    SsnProxyPlainMsg tag
end

transition ReDelegateStake(ssnaddr: ByStr20, to_ssn: ByStr20, amount: Uint128)
    tag = "ReDelegateStake";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    msg = let m = {
        _tag: tag;
        _recipient: proxy;
        _amount: zero;
        ssnaddr: ssnaddr;
        to_ssn: to_ssn;
        amount: amount
    } in one_msg m;
    send msg
end

transition RequestDelegatorSwap(new_deleg_addr: ByStr20)
    tag = "RequestDelegatorSwap";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    msg = let m = {
        _tag: tag;
        _recipient: proxy;
        _amount: zero;
        new_deleg_addr: new_deleg_addr
    } in one_msg m;
    send msg
end

transition RevokeDelegatorSwap()
    tag = "RevokeDelegatorSwap";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    SsnProxyPlainMsg tag
end

transition ConfirmDelegatorSwap(requestor: ByStr20)
    tag = "ConfirmDelegatorSwap";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    SsnProxyRequestorMsg tag requestor
end

transition RejectDelegatorSwap(requestor: ByStr20)
    tag = "RejectDelegatorSwap";
    IsAdmin; WalletIsNotLocked; PaybackForGas tag;
    SsnProxyRequestorMsg tag requestor
end





procedure DoSwapExactTokensForZIL(token_address: ByStr20, token_amount: Uint128, min_zil_amount: Uint128, deadline_block: BNum, commission: Uint128)
    UpdateDiscount; discount <- temp_discount;
    params = zilswap_params_sub_commission_with_discount token_amount min_zil_amount commission discount;
    match params with
    | Quadruplet token_amt token_comm zil_amt zil_comm =>
        
        PayTokenFee token_address token_comm;
        
        ReadByStr20Config zilswap; swap_addr <- bystr20_read;
        IncreaseAllowance token_address swap_addr token_amount;
        msg = let m = {
            _tag: "SwapExactTokensForZIL"; 
            _recipient: swap_addr;
            _amount: zero;
            token_address: token_address;
            token_amount: token_amt;
            min_zil_amount: zil_amt;
            deadline_block: deadline_block;
            recipient_address: _this_address
        } in one_msg m;
        send msg
    end
end

procedure DoSwapExactZILForTokens(token_address: ByStr20, zil_amount: Uint128, min_token_amount: Uint128, deadline_block: BNum, commission: Uint128)
    UpdateDiscount; discount <- temp_discount;
    params = zilswap_params_sub_commission_with_discount zil_amount min_token_amount commission discount;
    match params with
    | Quadruplet zil_amt zil_comm token_amt token_comm =>
        
        PayZILFee zil_comm;
        
        ReadByStr20Config zilswap; swap_addr <- bystr20_read;
        msg = let m = {
            _tag: "SwapExactZILForTokens"; 
            _recipient: swap_addr; 
            _amount: zil_amt; 
            token_address: token_address;
            min_token_amount: token_amt;
            deadline_block: deadline_block;
            recipient_address: _this_address
        } in one_msg m;
        send msg
    end
end

procedure DoSwapExactTokensForTokens(token0_address: ByStr20, token0_amount: Uint128, token1_address: ByStr20, min_token1_amount: Uint128, deadline_block: BNum, commission: Uint128)
    UpdateDiscount; discount <- temp_discount;
    params = zilswap_params_sub_commission_with_discount token0_amount min_token1_amount commission discount;
    match params with
    | Quadruplet token0_amt token0_comm token1_amt token1_comm =>
        
        PayTokenFee token0_address token0_comm;
        
        ReadByStr20Config zilswap; swap_addr <- bystr20_read;
        IncreaseAllowance token0_address swap_addr token0_amount;
        msg = let m = {
            _tag: "SwapExactTokensForTokens"; 
            _recipient: swap_addr; 
            _amount: zero; 
            token0_address: token0_address;
            token0_amount: token0_amt;
            token1_address: token1_address;
            min_token1_amount: token1_amt;
            deadline_block: deadline_block;
            recipient_address: _this_address
        } in one_msg m;
        send msg
    end
end

transition SwapExactTokensForTokens(token0_address: ByStr20, token0_amount: Uint128, token1_address: ByStr20, min_token1_amount: Uint128, deadline_block: BNum, commission: Uint128)
    g = "SwapExactTokensForTokens";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    DoSwapExactTokensForTokens token0_address token0_amount token1_address min_token1_amount deadline_block commission
end

transition SwapExactTokensForZIL(token_address: ByStr20, token_amount: Uint128, min_zil_amount: Uint128, deadline_block: BNum, commission: Uint128)
    g = "SwapExactTokensForZIL";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    DoSwapExactTokensForZIL token_address token_amount min_zil_amount deadline_block commission
end

transition SwapExactZILForTokens(token_address: ByStr20, zil_amount: Uint128, min_token_amount: Uint128, deadline_block: BNum, commission: Uint128)
    g = "SwapExactZILForTokens";    
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    params = zilswap_params_sub_commission_with_discount zil_amount min_token_amount commission;
    DoSwapExactZILForTokens token_address zil_amount min_token_amount deadline_block commission
end





transition VoidCheque(hash: ByStr)
    g = "VoidCheque";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    void_cheques[hash] := true
end
procedure ProcessZILTokenCheque(addr: ByStr20, amt1: Uint128, amt2: Uint128, deadline: BNum, commission: Uint128, signature: ByStr64)
    addr_hash = builtin sha256hash addr;
    amt1_hash = builtin sha256hash amt1;
    amt2_hash = builtin sha256hash amt2;
    deadline_hash = builtin sha256hash deadline;
    commission_hash = builtin sha256hash commission;
    this_addr_hash = builtin sha256hash _this_address;

    p0_hash = builtin concat addr_hash amt1_hash;
    p1_hash = builtin concat p0_hash amt2_hash;
    p2_hash = builtin concat p1_hash deadline_hash;
    p3_hash = builtin concat p2_hash commission_hash;
    p4_hash = builtin concat p3_hash this_addr_hash;
    
    admin_pub_temp <- admin_pubkey;
    cheque_hash = builtin to_bystr p4_hash;
    AssertChequeValidityAndVoidIt cheque_hash admin_pub_temp signature
end

transition MetaSwapExactZILForTokens(token_address: ByStr20, zil_amount: Uint128, min_token_amount: Uint128, deadline_block: BNum, commission: Uint128, signature: ByStr64)
    g = "MetaSwapExactZILForTokens";
    WalletIsNotLocked; PaybackForGas g;
    ProcessZILTokenCheque token_address zil_amount min_token_amount deadline_block commission signature;
    DoSwapExactZILForTokens token_address zil_amount min_token_amount deadline_block commission
end

transition MetaSwapExactTokensForZIL(token_address: ByStr20, token_amount: Uint128, min_zil_amount: Uint128, deadline_block: BNum, commission: Uint128, signature: ByStr64)
    g = "MetaSwapExactTokensForZIL";
    WalletIsNotLocked; PaybackForGas g;
    ProcessZILTokenCheque token_address token_amount min_zil_amount deadline_block commission signature;
    DoSwapExactTokensForZIL token_address token_amount min_zil_amount deadline_block commission
end

transition MetaSwapExactTokensForTokens(token0_address: ByStr20, token0_amount: Uint128, token1_address: ByStr20, min_token1_amount: Uint128, deadline_block: BNum, commission: Uint128, signature: ByStr64)
    g = "MetaSwapExactTokensForTokens";
    WalletIsNotLocked; PaybackForGas g; 
    
    token0_address_hash = builtin sha256hash token0_address;
    token0_amount_hash = builtin sha256hash token0_amount;
    token1_address_hash = builtin sha256hash token1_address;
    min_token1_amount_hash = builtin sha256hash min_token1_amount;
    deadline_block_hash = builtin sha256hash deadline_block;
    commission_hash = builtin sha256hash commission;
    this_addr_hash = builtin sha256hash _this_address;
    
    p0_hash = builtin concat token0_address_hash token0_amount_hash;
    p1_hash = builtin concat p0_hash token1_address_hash;
    p2_hash = builtin concat p1_hash min_token1_amount_hash;
    p3_hash = builtin concat p2_hash deadline_block_hash;
    p4_hash = builtin concat p3_hash commission_hash;
    p5_hash = builtin concat p4_hash this_addr_hash;
    
    admin_pub_temp <- admin_pubkey;
    cheque_hash = builtin to_bystr p5_hash;
    AssertChequeValidityAndVoidIt cheque_hash admin_pub_temp signature;
    DoSwapExactTokensForTokens token0_address token0_amount token1_address min_token1_amount deadline_block commission
end





procedure AssertValidRecoverySetup(pubkeys: List ByStr33, needed_signatures: Uint32)
    len = bystr33_list_len pubkeys;
    sig_lt_pub_len = builtin lt needed_signatures len;
    pub_len_is_zero = builtin eq uint32_zero len;
    sig_is_zero = builtin eq uint32_zero needed_signatures;
    e = InvalidSRSetup;
    match sig_lt_pub_len with
    | True =>
        match pub_len_is_zero with
        | False =>
            match sig_is_zero with
            | False =>
                are_unique = pubkeys_are_unique pubkeys;
                match are_unique with
                | True => 
                
                
                | False => er = PubkeysAreNotUnique; ThrowError er
                end
            | True => ThrowError e
            end
        | True => ThrowError e
        end
    | False => ThrowError e
    end
end

procedure VerifyAndVoidRecoveryMessage(msg: RecovererSignedMsg)
    new_admin <- temp_nominated_admin;
    cur_blk <- & BLOCKNUMBER;
    match msg with
    | RecovererSignedMsg pubkey valid_until sig =>
        sr <- recoverers;
        match sr with
        | Setup keys sigs_required =>
            
            is_from_recoverer = bystr33_has_elem keys pubkey; 
            not_expired = builtin blt cur_blk valid_until;
            is_valid = andb is_from_recoverer not_expired;
            match is_valid with
            | True =>
                
                new_admin_hash = builtin sha256hash new_admin;
                valid_until_hash = builtin sha256hash valid_until;
                this_addr_hash = builtin sha256hash _this_address;
                
                p0_hash = builtin concat new_admin_hash valid_until_hash;
                p1_hash = builtin concat p0_hash this_addr_hash;
                cheque_hash = builtin to_bystr p1_hash;
                AssertChequeValidityAndVoidIt cheque_hash pubkey sig
            | False => e = RecoveryMsgPreconditionNotMet; ThrowError e
            end
        | NotSetup => e = SRNotSetup; ThrowError e 
        end
    end
end

procedure AssertRecoveryMsgLenIsTheNeededLen(msgs: List RecovererSignedMsg, sigs_required: Uint32)
    len = recoverer_msg_list_len msgs;
    is_same = builtin eq len sigs_required;
    match is_same with
    | True => 
    | False => e = WrongRecoveryListLength; ThrowError e
    end
end
procedure AssertRecoveryMsgPubkeysAreUnique(msgs: List RecovererSignedMsg)
    are_unique = recoverer_msg_list_has_unique_pubkeys msgs;
    match are_unique with
    | True => 
    | False => e = PubkeysAreNotUnique; ThrowError e
    end
end
procedure SetNewAdmin(new_admin: ByStr33)
    admin_pubkey := new_admin;
    new_addr = builtin schnorr_get_address new_admin;
    admin := new_addr
end

transition RecoverWallet(msgs: List RecovererSignedMsg, nominated_admin: ByStr33)
    g = "RecoverWallet";
    PaybackForGas g;
    
    sr <- recoverers;
    match sr with
    | Setup keys sigs_required =>
        AssertRecoveryMsgLenIsTheNeededLen msgs sigs_required;
        AssertRecoveryMsgPubkeysAreUnique msgs;
        nominated_addr = builtin schnorr_get_address nominated_admin;
        temp_nominated_admin := nominated_addr;
        forall msgs VerifyAndVoidRecoveryMessage;
        SetNewAdmin nominated_admin;
        is_locked := false
    | NotSetup => e = SRNotSetup; ThrowError e 
    end
end

transition LockWallet()
    g = "LockWallet";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    sr <- recoverers;
    match sr with
    | Setup keys sigs_required => is_locked := true
    | NotSetup => e = SRNotSetup; ThrowError e 
    end
end

transition SetupRecovery(pubkeys: List ByStr33, needed_signatures: Uint32)
    g = "SetupRecovery";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    AssertValidRecoverySetup pubkeys needed_signatures;
    recoverers_tmp = Setup pubkeys needed_signatures;
    recoverers := recoverers_tmp
end




transition SellStakeToCookieJar(seller_ssn_addressses: List ByStr20, cookie_ssn_addressses: List ByStr20)
    g = "SellStakeToCookieJar";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    ReadByStr20Config ssn_proxy; proxy <- bystr20_read; 
    ReadByStr20Config cookie_jar; cookie <- bystr20_read;
    forall seller_ssn_addressses DoWithdrawStakeRewards;
    msg = let m = {
        _tag: "RequestDelegatorSwap"; 
        _recipient: proxy;
        _amount: zero;
        new_deleg_addr: cookie
    } in one_msg m;
    send msg;
    msg = let m = {
        _tag: "SellStakeToCookieJar"; 
        _recipient: cookie;
        _amount: zero;
        seller_ssn_addressses: seller_ssn_addressses;
        cookie_ssn_addressses: cookie_ssn_addressses
    } in one_msg m;
    send msg
end





transition WithdrawStakeRewardsSuccessCallBack(ssnaddr: ByStr20, rewards: Uint128)
    fee_tag = "WithdrawStakeRewardsSuccessCallBack_fee";
    
    ReadByStr20Config ssnlist_implementation; ssnlist <- bystr20_read;
    is_ssnlist = builtin eq ssnlist _sender;
    was_service_account <- temp_was_service_account;
    temp_was_service_account := false;
    ReadUint128Config fee_tag; percentage_fee <- uint128_read;
    is_zero = builtin eq zero percentage_fee;
    not_zero = negb is_zero;
    is_ssnlist_was_service_account = andb was_service_account is_ssnlist;
    is_ssnlist_was_service_account_fee_not_zero = andb is_ssnlist_was_service_account not_zero;
    
    match is_ssnlist_was_service_account_fee_not_zero with
    | True => 
        
        rewards_and_fee = sub_percentage_of_amt rewards percentage_fee;
        match rewards_and_fee with
        | Uint128Pair rewards_to_keep fee_to_pay =>
            PayZILFee fee_to_pay
        end
    | False => 
    end;
    
    rep <-& init_config.reputation;
    msg = let m = { 
        _tag: "MintMeReputation";
        _recipient: rep;
        _amount: zero
    } in one_msg m;
    send msg
end


transition MoveToNewAddress(new_address: ByStr20 with contract field admin: ByStr20 end)
    g = "MoveToNewAddress";
    IsAdmin; WalletIsNotLocked; PaybackForGas g;
    
    other_admin <-& new_address.admin;
    AssertIsAdmin other_admin;
    
    rep <-& init_config.reputation;
    msg = let m = {
        _tag: "MoveToNewAddress";
        _recipient: rep;
        _amount: zero;
        new_address: new_address
    } in one_msg m;
    send msg
end

transition DelegateStakeSuccessCallBack(ssnaddr: ByStr20, amount: Uint128) end
transition WithdrawStakeAmtSuccessCallBack(ssnaddr: ByStr20, amount: Uint128) end
transition CompleteWithdrawalNoUnbondedStakeCallBack(amount: Uint128) end
transition CompleteWithdrawalSuccessCallBack(amount: Uint128) end
transition ReDelegateStakeSuccessCallBack(ssnaddr: ByStr20, tossn: ByStr20, amount: Uint128) end

transition RecipientAcceptMint(minter: ByStr20, recipient: ByStr20, amount: Uint128) end
transition RecipientAcceptTransfer(sender: ByStr20, recipient: ByStr20, amount: Uint128) end
transition RecipientAcceptTransferFrom(initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128) end
transition TransferSuccessCallBack(sender: ByStr20, recipient: ByStr20, amount : Uint128) end
transition TransferFromSuccessCallBack(initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128) end

transition AddFunds() accept end